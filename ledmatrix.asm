;
; pe22_ex3.asm
;
; Created: 10/12/2023 12:28:34 PM
; Author : tuan minh
;


; Replace with your application code
.ORG 0
.EQU SHIFT_CLK = 0
.EQU SHIFT_SDI = 1
.EQU SHIFT_LATCH = 2
.EQU SHIFT_nCLR = 3
.DEF TEMP = R16
.DEF COUNTER = R17
.DEF SHIFT_COUNTER = R18
.DEF COLUMNREG = R21
.DEF DATA_REG = R22
.EQU MATRIX_COL = PORTA
.EQU SHIFT_PORT = PORTB
.EQU MATRIX_DDR = DDRA
.EQU SHIFT_DDR = DDRB

INIT:
		RCALL INIT_PORT

MAIN:
		RCALL INIT_MATRIX ; MATRIX DATA OUTPUT INITIALIZATION

DISPLAY:
		RCALL MATRIX_DISPLAY ; DISPLAY ROW VALUE FOR RESPECTIVE COLUMN
		RCALL DELAYT1_5ms ; SCANNING FREQUENCY
		DEC COUNTER 
		BRNE DISPLAY
		RJMP MAIN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
INIT_PORT:
		LDI TEMP, 0xFF
		OUT MATRIX_DDR, TEMP
		LDI TEMP, 0x0F
		OUT SHIFT_DDR, TEMP
		SBI SHIFT_PORT, SHIFT_nCLR ; ALWAYS ENABLE DATA IN SHIFT REGISTER
		RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
INIT_MATRIX:
		LDI COUNTER, 8
		LDI TEMP, HIGH(DATA << 1)
		MOV ZH, TEMP
		LDI TEMP, LOW(DATA << 1)
		MOV ZL, TEMP   
		RET

DATA:
.DB 0b11111100, 0x80, 0b00010010, 0x40, 0b00010001, 0x20, 0b00010001, 0x10, 0b00010010, 0x08, 0b11111100, 0x04, 0b00000000, 0x02, 0b00000000, 0x01 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
MATRIX_DISPLAY:		
		LPM DATA_REG, Z+
		LDI SHIFT_COUNTER, 8
		CBI SHIFT_PORT, SHIFT_LATCH

SHIFT_DATA:
		CBI SHIFT_PORT, SHIFT_CLK
		LSL DATA_REG
		BRCS HIGH_DATA
		CBI SHIFT_PORT, SHIFT_SDI
		RJMP SET_CLK

HIGH_DATA:
		SBI SHIFT_PORT, SHIFT_SDI

SET_CLK:
		SBI SHIFT_PORT, SHIFT_CLK
		RCALL SDELAY
		CBI SHIFT_PORT, SHIFT_CLK
		DEC SHIFT_COUNTER
		BRNE SHIFT_DATA

		SBI SHIFT_PORT, SHIFT_LATCH
		RCALL SDELAY
		CBI SHIFT_PORT, SHIFT_LATCH

		LPM COLUMNREG, Z+
		OUT MATRIX_COL, COLUMNREG

		RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
DELAYT1_5ms:
		LDI TEMP, 0b00001001 ; CTC MODE, PRESCALER CLK/64
		STS TCCR1B, TEMP
		LDI TEMP, 0x40
		STS OCR1AH, TEMP
		LDI TEMP, 0x00
		STS OCR1AL, TEMP
		CLR TEMP
		STS TCNT1H, TEMP
		STS TCNT1L, TEMP

DELAYT1_5ms_LOOP:
		SBIS TIFR1, OCF1A
		RJMP DELAYT1_5ms_LOOP
		SBI TIFR1, OCF1A ; RESET TIMER1
		RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SDELAY:
		NOP
		NOP
		RET		
